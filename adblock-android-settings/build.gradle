apply plugin: 'com.android.library'
apply plugin: 'com.novoda.bintray-release'
apply plugin: 'kotlin-android'

android {
  compileSdkVersion 28
  buildToolsVersion '28.0.3'

  defaultConfig {
    minSdkVersion 16
    targetSdkVersion 28
    missingDimensionStrategy 'abi', 'abi_all', 'abi_arm', 'abi_arm64', 'abi_x86'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  packagingOptions {
    exclude 'META-INF/proguard/androidx-annotations.pro'
  }

  sourceSets {
    test.java.srcDirs += 'src/test/kotlin'
    androidTest.java.srcDirs += 'src/androidTest/kotlin'
  }

  useLibrary 'android.test.runner'
  useLibrary 'android.test.base'
}

dependencies {
  api project(':adblock-android')
  api 'com.android.support:preference-v14:28.0.0'
  testImplementation 'junit:junit:4.12'
  testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  androidTestImplementation 'androidx.test:core:1.1.0'
  androidTestImplementation 'androidx.test:runner:1.1.0'
  androidTestImplementation 'androidx.test.ext:junit:1.1.0'
}

// Add "android.jar" to javadoc plugin classpath (See DP-612)
afterEvaluate {
  def androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
  tasks.withType(Javadoc).all {
    classpath += project.files(androidJar)
  }
}

publish {
  userOrg = rootProject.ext.bintrayUserOrg
  groupId = rootProject.ext.bintrayGroupId
  artifactId = 'adblock-android-settings'
  publishVersion = rootProject.ext.moduleVersion
  licences = rootProject.ext.bintrayLicences
  desc = 'An Android library that provides a configuration interface for Adblock Plus.'
  website = rootProject.ext.bintrayWebsite
  issueTracker = rootProject.ext.bintrayIssueTracker
  repository = rootProject.ext.bintrayRepository
}
